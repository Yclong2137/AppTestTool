apply plugin: 'groovy'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    google()
}

sourceSets {
    main {
        groovy {
            srcDir 'src/main/groovy'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

//获取maven私服账户名
def getRepositoryUsername() {
    return hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""
}

//获取maven私服密码
def getRepositoryPassword() {
    return hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""
}


//发布前先执行 clean, 避免发布的aar中使用了旧的缓存代码
publish.dependsOn clean

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId = GROUP
                artifactId = POM_ARTIFACT_ID
                version = VERSION_COMMON_NAME

                from components.java
            }
        }

        repositories {
            mavenLocal()
            maven {
                name = "Qisi"
                url = VERSION_COMMON_NAME.endsWith("SNAPSHOT") ? SNAPSHOT_REPOSITORY_URL : RELEASE_REPOSITORY_URL

                credentials {
                    username getRepositoryUsername()
                    password getRepositoryPassword()
                }
            }

        }
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'com.android.tools.build:gradle:4.1.3'
    implementation 'org.ow2.asm:asm:9.2'
}

